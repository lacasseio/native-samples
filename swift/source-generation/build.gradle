plugins {
    id 'swift-application'
    id 'xcode'
}

def codeGeneration = tasks.register("codeGeneration", CodeGenerator) {
    generatedDir = project.layout.buildDirectory.dir("generated/swift")
}

application {
    source.from codeGeneration.flatMap { it.generatedDir }
}

// Code generation implementation could be in another plugin or buildSrc
abstract class CodeGenerator extends DefaultTask {
    @OutputDirectory
    abstract DirectoryProperty getGeneratedDir()

    @TaskAction
    void generate() {
        generateSources(generatedDir.getAsFile().get())
    }

    private void generateSources(File generatedDir) {
        new File(generatedDir, "main.swift").text = """
internal func getMessage() -> String {
    return "Hello, World!"
}

// Simple hello world app
print(getMessage())
        """
    }
}
